import org.grails.forge.internal.tasks.WriteGrailsVersionInfoTask

plugins {
    id "org.grails.forge.internal.published-module"
    id "org.grails.forge.internal.convention"
    id "org.grails.forge.rocker"
}

sourceSets {
    main {
        rocker {
            srcDir("src/main/java")
        }
    }
}

dependencies {
    api platform("io.micronaut:micronaut-bom:$micronautVersion")
    api "com.fizzed:rocker-runtime:$rockerVersion"
    api "io.micronaut:micronaut-inject"
    api "io.micronaut:micronaut-http"
    api "io.micronaut:micronaut-http-client"
    api "io.github.java-diff-utils:java-diff-utils:$javaDiffUtils"
    api "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    implementation "org.yaml:snakeyaml:$snakeyamlVersion"
    implementation "com.typesafe:config:$typesafeConfigVersion"
    implementation "org.apache.commons:commons-compress:$commonsCompressVersion"
    compileOnly "com.google.code.findbugs:jsr305"
    testImplementation "org.codehaus.groovy:groovy-yaml"
}


def grailsVersionInfo = tasks.register("grailsVersionInfo", WriteGrailsVersionInfoTask) {
    version = grailsVersion
    outputDirectory = layout.buildDirectory.dir("version-info")
}

sourceSets.main.resources.srcDir(grailsVersionInfo)

rocker {
    postProcessing = ['org.grails.forge.rocker.plugin.WhitespaceProcessor']
}

nohttp {
    source.exclude "**/pom.rocker.raw"
}

javadoc {
    options.encoding = 'UTF-8'
}

repositories {
    maven { url "https://repo.grails.org/grails/core" }
    mavenCentral()
}