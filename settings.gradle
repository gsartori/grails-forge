pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}
buildscript {
    dependencies {
        classpath 'org.antlr:antlr4-runtime:4.8-1!!'
    }
}
plugins {
    id "com.gradle.develocity" version "3.18.2"
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.2'
}

def isCI = System.getenv('CI') != null
def isLocal = !isCI
def isAuthenticated = System.getenv('DEVELOCITY_ACCESS_KEY') != null
def isBuildCacheAuthenticated =
        System.getenv('DEVELOCITY_BUILD_CACHE_NODE_USER') != null &&
        System.getenv('DEVELOCITY_BUILD_CACHE_NODE_KEY') != null

develocity {
    server = 'https://ge.grails.org'
    buildScan {
        publishing.onlyIf { isAuthenticated }
        uploadInBackground = isLocal
    }
}

buildCache {
    local { enabled = isLocal }
    remote(develocity.buildCache) {
        push = isCI && isBuildCacheAuthenticated
        enabled = true
        usernameAndPassword(
                System.getenv('DEVELOCITY_BUILD_CACHE_NODE_USER') ?: '',
                System.getenv('DEVELOCITY_BUILD_CACHE_NODE_KEY') ?: ''
        )
    }
}

rootProject.name = 'grails-forge'

include 'grails-forge-core'
include 'grails-cli'
include 'test-core'
include 'grails-forge-api'
include 'grails-forge-web-netty'
include 'grails-forge-analytics-postgres'