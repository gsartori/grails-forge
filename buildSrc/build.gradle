plugins {
    id "java-gradle-plugin"
    id "groovy-gradle-plugin"
}

Properties properties = new Properties()
file('../gradle.properties').withInputStream { input ->
    properties.load(input)
}

for (String key : properties.stringPropertyNames()) {
    ext.set(key, properties.getProperty(key))
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}


configurations.all {
    exclude group: "io.github.groovylang.groovydoc", module: "io.github.groovylang.groovydoc.gradle.plugin"
}

dependencies {
    implementation "io.micronaut.build.internal:micronaut-gradle-plugins:$micronautGradlePlugins"
    implementation "com.fizzed:rocker-compiler:$rockerVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
    implementation "io.sdkman:gradle-sdkvendor-plugin:$gradleSdkvendorPluginVersion"
    implementation "org.asciidoctor:asciidoctor-gradle-jvm:$asciidoctorGradleJvmVersion"
    implementation "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    implementation "io.spring.nohttp:nohttp-gradle:$nohttpGradleVersion"
    implementation "gradle.plugin.com.github.johnrengelman:shadow:$shadowVersion"
    implementation "io.github.gradle-nexus:publish-plugin:$nexusPublishVersion"
}

gradlePlugin {
    plugins {
        rockerPlugin {
            id = "org.grails.forge.rocker"
            implementationClass = "org.grails.forge.rocker.plugin.RockerPlugin"
        }
    }
}
